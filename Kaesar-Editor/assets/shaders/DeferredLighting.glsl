// Basic diffuse Shader
#type vertex

#version 460
	
layout(location = 0) in vec3 a_Position;
layout(location = 1) in vec2 a_TexCoords;

layout(location = 0) out vec2 v_TexCoords;

void main()
{
	v_TexCoords = a_TexCoords;

	gl_Position = vec4(a_Position, 1.0);
}

#type fragment

#version 460

layout(location = 0) out vec4 FragColor;

// GBuffer samplers
layout(binding = 0) uniform sampler2D gPosition;
layout(binding = 1) uniform sampler2D gNormal;
layout(binding = 2) uniform sampler2D gAlbedoSpec;
layout(binding = 6) uniform sampler2D gRoughMetalAO;

// Shadow related samplers
layout(binding = 3) uniform sampler2DShadow shadowMap;
layout(binding = 4) uniform sampler1D distribution0;
layout(binding = 5) uniform sampler1D distribution1;

// IBL
layout(binding = 7) uniform samplerCube irradianceMap;
layout(binding = 8) uniform samplerCube prefilterMap;
layout(binding = 9) uniform sampler2D   brdfLUT;

//-----------------------------------------------UNIFORM BUFFERS-----------------------------------------//

layout(binding = 0) uniform Camera
{
	mat4 u_ViewProjection;
	vec3 u_CameraPos;
} camera;

layout(binding = 1) uniform Transform
{
    mat4 u_Trans;
	int u_ID;
} transform;

layout(binding = 4) uniform Shadow
{
	mat4 u_LightViewProjection;
} shadow;

struct DLight
{
    vec4 direction;
    vec4 color;
};

struct PLight
{
    vec4 position;
    vec4 color;
};

struct SLight
{
    vec4 position; 
    vec4 direction;
    vec4 color;
};

struct LightsParams
{
    float pointLinear;
    float pointQuadratic;

    float spotLinear;
    float spotQuadratic;
    float innerCutOff;
    float outerCutOff;
};

layout(binding = 2) uniform Lights
{
    PLight pointLight[5];
    SLight spotLight[5];
    DLight directionalLight;
} lights;

layout(binding = 3) uniform Params
{
    LightsParams lightsParams[5];
} params;

//-----------------------------------------------PUSH CONSTANT-----------------------------------------//
layout(push_constant) uniform pushConstants
{
    float exposure;
	float gamma;
	float size;
    float intensity;
    float near; // é˜´å½±æ˜ å°„ä¸­çš„é˜´å½±æœç´¢èŒƒå›´çš„è¿‘ç«¯
    int numPCFSamples; // é˜´å½±æ˜ å°„ä¸­çš„ PCF æ ·æœ¬æ•°
    int numBlockerSearchSamples; // é˜´å½±æ˜ å°„ä¸­çš„é˜´å½±æœç´¢æ ·æœ¬æ•°
    int softShadow; // æ˜¯å¦å¯ç”¨è½¯é˜´å½±
} pc;

struct VS_OUT 
{
	vec3 v_FragPos;
	vec3 v_Normal;
	vec2 v_TexCroods;
	vec4 v_FragPosLightSpace;
};

layout(location = 0) in vec2 v_TexCoords;

//-----------------------------------------Shadow calculation functions-------------------------------//
// é€šè¿‡åœ¨æŒ‡å®šçš„åˆ†å¸ƒä¸­è¿›è¡Œé‡‡æ ·ï¼Œç”Ÿæˆä¸€ä¸ªåœ¨äºŒç»´å¹³é¢ä¸Šçš„éšæœºæ–¹å‘å‘é‡
vec2 RandomDirection(sampler1D distribution, float u)
{
    // ä»é‡‡æ ·å™¨ sampler1D ä¸­ä½¿ç”¨å‚æ•° u è¿›è¡Œé‡‡æ ·ï¼Œè¿”å›ä¸€ä¸ªäºŒç»´å‘é‡ï¼Œå…¶ä¸­ x åˆ†é‡å’Œ y åˆ†é‡åˆ†åˆ«å­˜å‚¨äº†ä»é‡‡æ ·å™¨ä¸­è·å–çš„æ•°æ®
    // å°†æå–çš„äºŒç»´å‘é‡çš„æ¯ä¸ªåˆ†é‡éƒ½ä¹˜ä»¥ 2ï¼Œä»è€Œå°†åˆ†é‡çš„èŒƒå›´ä» [0, 1] æ˜ å°„åˆ° [0, 2]
    // å°†å¾—åˆ°çš„å‘é‡çš„æ¯ä¸ªåˆ†é‡éƒ½å‡å» 1ï¼Œå°†èŒƒå›´ä» [0, 2] æ˜ å°„åˆ° [-1, 1]
   return texture(distribution, u).xy * 2 - vec2(1);
}

/// è®¡ç®—åœ¨æŸä¸ªå…‰æºåˆ°æ¥æ”¶å™¨ä¹‹é—´æœç´¢å®½åº¦
/// params uvLightSize è¡¨ç¤ºå…‰æºçš„å¤§å°ï¼Œé€šå¸¸åœ¨çº¹ç†åæ ‡ä¸­å®šä¹‰
/// params receiverDistance è¡¨ç¤ºæ¥æ”¶å™¨ï¼ˆé€šå¸¸æ˜¯ç›¸æœºæˆ–ç‰©ä½“ï¼‰ä¸å…‰æºä¹‹é—´çš„è·ç¦»
/// return æœç´¢å®½åº¦
float SearchWidth(float uvLightSize, float receiverDistance)
{
	return uvLightSize * (receiverDistance - pc.near) / receiverDistance;
}

/// è®¡ç®—å¹³è¡Œå…‰æºä¸‹é˜´å½±æ˜ å°„ä¸­çš„é®æŒ¡è·ç¦»
/// params shadowCoords è¡¨ç¤ºä»ç‰‡æ®µç€è‰²å™¨ä¸­è®¡ç®—å‡ºçš„é˜´å½±åæ ‡
/// params shadowMap é˜´å½±è´´å›¾çš„é‡‡æ ·å™¨ï¼Œç”¨äºä»è´´å›¾ä¸­è·å–æ·±åº¦ä¿¡æ¯
/// params uvLightSize è¡¨ç¤ºå…‰æºçš„å¤§å°ï¼Œé€šå¸¸åœ¨çº¹ç†åæ ‡ä¸­å®šä¹‰
/// params bias è¡¨ç¤ºé˜´å½±æ˜ å°„ä¸­çš„åç§»é‡
/// return é®æŒ¡è·ç¦»
float FindBlockerDistance_DirectionalLight(vec3 shadowCoords, sampler2DShadow shadowMap, float uvLightSize, float bias)
{
	int blockers = 0; // é®æŒ¡è€…çš„æ•°é‡
	float avgBlockerDistance = 0; // é®æŒ¡è€…çš„æ·±åº¦å€¼

    // è®¡ç®—æœç´¢å®½åº¦ï¼Œç”¨äºç¡®å®šåœ¨é˜´å½±è´´å›¾ä¸­æœç´¢é®æŒ¡è€…çš„èŒƒå›´
	float searchWidth = SearchWidth(uvLightSize, shadowCoords.z);

    // ä»é˜´å½±è´´å›¾ä¸­æœç´¢é®æŒ¡è€…
	for (int i = 0; i < pc.numBlockerSearchSamples; i++)
	{
        // è®¡ç®—ç”¨äºé‡‡æ ·çš„çº¹ç†åæ ‡ï¼Œä½¿ç”¨äº† RandomDirection å°†éšæœºæ–¹å‘ä¸å…‰æºå¤§å°ç»“åˆï¼Œä»¥ä¾¿åœ¨å…‰æºåŒºåŸŸå†…éšæœºé‡‡æ ·
		vec3 uvc = vec3(shadowCoords.xy + RandomDirection(distribution0, i / float(pc.numPCFSamples)) * uvLightSize, (shadowCoords.z - bias));
		float z = texture(shadowMap, uvc); // ä»é˜´å½±è´´å›¾ä¸­è·å–é®æŒ¡è€…çš„æ·±åº¦
		
        if (z < 0.5) // å¦‚æœé®æŒ¡è€…çš„æ·±åº¦å°äºå½“å‰ç‰‡æ®µçš„æ·±åº¦ï¼Œåˆ™è¡¨ç¤ºè¯¥ç‰‡æ®µè¢«é®æŒ¡ï¼Œä½†æ˜¯å¦‚æœå’Œ (shadowCoords.z - bias) æ¯”è¾ƒï¼Œé˜´å½±èµ°æ ·å¾ˆä¸¥é‡ğŸ˜… å’Œ 0.5 æ¯”è¾ƒç»“æœè¦å¥½å¾—å¤šğŸ˜… Why? ğŸ¤”
		{
			blockers++;
			avgBlockerDistance += z;
		}
	}

	if (blockers > 0)
		return avgBlockerDistance / blockers;
	else
		return -1;
}

/// è®¡ç®—å¹³è¡Œå…‰æºä¸‹é˜´å½±æ˜ å°„ä¸­çš„ PCF é˜´å½±
/// params shadowCoords è¡¨ç¤ºä»ç‰‡æ®µç€è‰²å™¨ä¸­è®¡ç®—å‡ºçš„é˜´å½±åæ ‡
/// params shadowMap é˜´å½±è´´å›¾çš„é‡‡æ ·å™¨ï¼Œç”¨äºä»è´´å›¾ä¸­è·å–æ·±åº¦ä¿¡æ¯
/// params uvRadius è¡¨ç¤ºå…‰æºçš„åŠå¾„ï¼Œé€šå¸¸åœ¨çº¹ç†åæ ‡ä¸­å®šä¹‰
/// params bias è¡¨ç¤ºé˜´å½±æ˜ å°„ä¸­çš„åç§»é‡
float PCF_DirectionalLight(vec3 shadowCoords, sampler2DShadow shadowMap, float uvRadius, float bias)
{
	float sum = 0; // å­˜å‚¨å¤šä¸ªé‡‡æ ·ç‚¹çš„æ·±åº¦ä¹‹å’Œ

    // åœ¨é˜´å½±è´´å›¾ä¸­è¿›è¡Œå¤šæ¬¡é‡‡æ ·ï¼Œä»¥è®¡ç®—å¹³å‡æ·±åº¦
	for (int i = 0; i < pc.numPCFSamples; i++)
	{
        // åœ¨å…‰æºåŒºåŸŸå†…éšæœºé‡‡æ ·
		vec3 uvc = vec3(shadowCoords.xy + RandomDirection(distribution1, i / float(pc.numPCFSamples)) * uvRadius, (shadowCoords.z - bias));
		float z = texture(shadowMap, uvc);
		sum += z;
	}

	return sum / pc.numPCFSamples;
}

/// è®¡ç®—å¹³è¡Œå…‰æºä¸‹é˜´å½±æ˜ å°„ä¸­çš„ PCSS é˜´å½±
/// params shadowCoords è¡¨ç¤ºä»ç‰‡æ®µç€è‰²å™¨ä¸­è®¡ç®—å‡ºçš„é˜´å½±åæ ‡
/// params shadowMap é˜´å½±è´´å›¾çš„é‡‡æ ·å™¨ï¼Œç”¨äºä»è´´å›¾ä¸­è·å–æ·±åº¦ä¿¡æ¯
/// params uvLightSize è¡¨ç¤ºå…‰æºçš„å¤§å°ï¼Œé€šå¸¸åœ¨çº¹ç†åæ ‡ä¸­å®šä¹‰
/// params bias è¡¨ç¤ºé˜´å½±æ˜ å°„ä¸­çš„åç§»é‡
float PCSS_DirectionalLight(vec3 shadowCoords, sampler2DShadow shadowMap, float uvLightSize, float bias)
{
	// Blocker search
	float blockerDistance = FindBlockerDistance_DirectionalLight(shadowCoords, shadowMap, uvLightSize, bias);

	if (blockerDistance == -1) // æ²¡æœ‰é®æŒ¡è€…ï¼Œä¸å­˜åœ¨é˜´å½±
		return 0;		

	// Penumbra estimation
	float penumbraWidth = (shadowCoords.z - blockerDistance) / blockerDistance;

	// PCF
	float uvRadius = penumbraWidth * uvLightSize * pc.near / shadowCoords.z; // è®¡ç®—ç”¨äº PCF é‡‡æ ·çš„åŠå¾„

	return PCF_DirectionalLight(shadowCoords, shadowMap, uvRadius, bias);
}

float SoftShadowCalculation(vec4 fragPosLightSpace, float bias)
{
    // æ‰§è¡Œé€è§†é™¤æ³•ï¼Œè½¬ä¸º NDC åæ ‡
    // å½“åœ¨é¡¶ç‚¹ç€è‰²å™¨è¾“å‡ºä¸€ä¸ªè£åˆ‡ç©ºé—´é¡¶ç‚¹ä½ç½®åˆ° gl_Position æ—¶ï¼ŒOpenGL è‡ªåŠ¨è¿›è¡Œé€è§†é™¤æ³•ï¼Œå°†è£åˆ‡ç©ºé—´åæ ‡çš„èŒƒå›´ -w åˆ° w è½¬ä¸º -1 åˆ° 1ã€‚
    // ç”±äºè£åˆ‡ç©ºé—´çš„ FragPosLightSpace å¹¶ä¸ä¼šé€šè¿‡ gl_Position ä¼ åˆ°ç‰‡æ®µç€è‰²å™¨é‡Œï¼Œæ‰€ä»¥å¿…é¡»è‡ªå·±åšé€è§†é™¤æ³•
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    // è½¬ä¸º [0, 1] èŒƒå›´çš„åæ ‡
    projCoords = projCoords * 0.5 + 0.5;

    // å–å¾—å½“å‰ç‰‡æ®µåœ¨å…‰æºè§†è§’ä¸‹çš„æ·±åº¦
    float currentDepth = projCoords.z;
    
    float shadow = 0;

    if(projCoords.z > 1.0) // è¶…å‡ºå…‰æºè§†é”¥ï¼Œä¸è€ƒè™‘é˜´å½±
        shadow = 0.0;

    shadow = PCSS_DirectionalLight(projCoords, shadowMap, pc.size, bias);

    return shadow;
}

float HardShadowCalculation(vec4 fragPosLightSpace, float bias)
{
    // æ‰§è¡Œé€è§†é™¤æ³•ï¼Œè½¬ä¸º NDC åæ ‡
    // å½“åœ¨é¡¶ç‚¹ç€è‰²å™¨è¾“å‡ºä¸€ä¸ªè£åˆ‡ç©ºé—´é¡¶ç‚¹ä½ç½®åˆ° gl_Position æ—¶ï¼ŒOpenGL è‡ªåŠ¨è¿›è¡Œé€è§†é™¤æ³•ï¼Œå°†è£åˆ‡ç©ºé—´åæ ‡çš„èŒƒå›´ -w åˆ° w è½¬ä¸º -1 åˆ° 1ã€‚
    // ç”±äºè£åˆ‡ç©ºé—´çš„ FragPosLightSpace å¹¶ä¸ä¼šé€šè¿‡ gl_Position ä¼ åˆ°ç‰‡æ®µç€è‰²å™¨é‡Œï¼Œæ‰€ä»¥å¿…é¡»è‡ªå·±åšé€è§†é™¤æ³•
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    // è½¬ä¸º [0, 1] èŒƒå›´çš„åæ ‡
    projCoords = projCoords * 0.5 + 0.5;

    // å–å¾—å½“å‰ç‰‡æ®µåœ¨å…‰æºè§†è§’ä¸‹çš„æ·±åº¦
    float currentDepth = projCoords.z;

    float shadow = 0.0;
    // textureSize è¿”å›ä¸€ä¸ªç»™å®šé‡‡æ ·å™¨çº¹ç†çš„ 0 çº§ mipmap çš„ vec2 ç±»å‹çš„å®½å’Œé«˜
    // ç”¨ 1 é™¤ä»¥å®ƒè¿”å›ä¸€ä¸ªå•ç‹¬çº¹ç†åƒç´ çš„å¤§å°
    vec2 texelSize = 1.0 / textureSize(shadowMap, 0);

    for (int x = -1; x <= 1; ++x)
    {
        for (int y = -1; y <= 1; ++y)
        {
            // æ£€æŸ¥å½“å‰ç‰‡æ®µå‘¨å›´ 9 ä¸ªç‰‡æ®µçš„æ·±åº¦å€¼
            vec3 uvc = vec3(projCoords.xy + vec2(x, y) * texelSize, (projCoords.z - bias));
            float pcfDepth = texture(shadowMap, uvc);
            shadow += (pcfDepth < (projCoords.z - bias)) ? 1 : 0;
        }
    }

    // å–å¹³å‡å€¼
    shadow /= 9.0;

    return shadow;
}

//--------------------------------------------------PBR------------------------------------------------//

const float PI = 3.14159265359;

/// æ³•çº¿åˆ†å¸ƒå‡½æ•° D (Trowbridge-Reitz GGX)
/// params N æ³•çº¿
/// params H å…¥å°„å…‰çº¿ä¸è§†çº¿çš„åŠç¨‹å‘é‡
/// params roughness ç²—ç³™åº¦
/// return åˆ†å¸ƒå‡½æ•°å€¼
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a2 = roughness * roughness;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;

    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return a2 / denom;
}

/// å‡ ä½•å‡½æ•° G (Schlick GGX)
/// params NdotV æ³•çº¿ä¸è§†çº¿çš„ç‚¹ç§¯
/// params roughness ç²—ç³™åº¦
/// return å‡ ä½•å‡½æ•°å€¼
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r * r) / 8.0;

    float nom = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

/// å‡ ä½•å‡½æ•° G (Smith Joint GGX)
/// params N æ³•çº¿
/// params V è§†çº¿
/// params L å…¥å°„å…‰çº¿
/// params roughness ç²—ç³™åº¦
/// return å‡ ä½•å‡½æ•°å€¼
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, roughness);
    float ggx2 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}

/// è²æ¶…å°”æ–¹ç¨‹ F (Fresnel-Schlick è¿‘ä¼¼)
/// params cosTheta åŠç¨‹å‘é‡ä¸è§†çº¿ï¼ˆæˆ–æ³•çº¿ï¼‰ä¹‹é—´çš„å¤¹è§’çš„ä½™å¼¦å€¼
/// params F0 æè´¨çš„åŸºç¡€åå°„ç‡
/// return è²æ¶…å°”åå°„ç‡
vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(max(1.0 - cosTheta, 0.0), 5.0);
}

vec3 FresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(max(1.0 - cosTheta, 0.0), 5.0);
}

/// è®¡ç®—è¡¨é¢çš„å…‰ç…§é¢œè‰²
/// params L å…¥å°„å…‰çº¿æ–¹å‘å‘é‡
/// params N è¡¨é¢æ³•çº¿å‘é‡
/// params V è§†çº¿æ–¹å‘å‘é‡
/// params Ra ç¯å¢ƒå…‰é¢œè‰²
/// params F0 æè´¨çš„åŸºç¡€åå°„ç‡
/// params R ç²—ç³™åº¦å‚æ•°
/// params M é‡‘å±åº¦å‚æ•°
/// params A ç¯å¢ƒå…‰é¢œè‰²
/// return è®¡ç®—å¾—åˆ°çš„å…‰ç…§é¢œè‰²
vec3 CalculateL0(vec3 L, vec3 N, vec3 V, vec3 Ra, vec3 F0, float R, float M, vec3 A)
{
    vec3 H = normalize(L + V); // åŠç¨‹å‘é‡

    // Cook-Torrance BRDF
    float D = DistributionGGX(N, H, R);
    float G = GeometrySmith(N, V, L, R);
    vec3  F = FresnelSchlick(max(dot(H, V), 0.0), F0);

    vec3 F_lambert = A / PI; // æ¼«åå°„é¡¹

    vec3 Nominator = D * G * F;
    float Denominator = 4 * max(dot(V, N), 0.0) * max(dot(L, N), 0.0) + 0.001;
    vec3 F_cook = Nominator / Denominator; // é•œé¢åå°„é¡¹

    vec3 Ks = F; // åå°„å…‰çº¿æ‰€å æ¯”ç‡
    vec3 Kd = vec3(1.0) - Ks; // æŠ˜å°„ï¼ˆæ¼«åå°„ï¼‰å…‰çº¿æ‰€å æ¯”ç‡

    Kd *= 1.0 - M; // è€ƒè™‘é‡‘å±åº¦çš„è°ƒæ•´

	float NdotL = max(dot(N, L), 0.0);

    vec3 FR = Kd * F_lambert + Ks * F_cook;

    return FR * Ra * NdotL;
}

void main()
{	
    vec3 fragPos    = texture(gPosition, v_TexCoords).rgb;
	vec3 N          = texture(gNormal, v_TexCoords).rgb;
	vec3 Albedo     = pow(texture(gAlbedoSpec, v_TexCoords).rgb, vec3(pc.gamma)); // Gamma çŸ«æ­£
	float Roughness = texture(gRoughMetalAO, v_TexCoords).r;
	float Metallic  = texture(gRoughMetalAO, v_TexCoords).g;
	float AO		= texture(gRoughMetalAO, v_TexCoords).b;

    vec3 V = normalize(camera.u_CameraPos - fragPos);
    vec3 R = reflect(-V, N);

    vec3 F0 = vec3(0.04);
    F0 = mix(F0, Albedo, Metallic);

    vec3 L0 = vec3(0.0);

    vec4 FragPosLightSpace = shadow.u_LightViewProjection * vec4(fragPos, 1.0);

    vec3  L = normalize(-lights.directionalLight.direction.rgb);
    float bias = max(0.01 * (1.0 - dot(N, L)), 0.001);

    L0 += CalculateL0(L, N, V, lights.directionalLight.color.rgb, F0, Roughness, Metallic, Albedo);

    for (int i = 0; i < 5; i++)
    {
        vec3 L = normalize(lights.pointLight[i].position.rgb - fragPos);
        float dist = length(lights.pointLight[i].position.rgb - fragPos);
        float attenuation = 1.0 / (1.0 + dist * (params.lightsParams[i].pointLinear + params.lightsParams[i].pointQuadratic * dist));

        vec3 Ra = lights.pointLight[i].color.rgb * attenuation;

        L0 += CalculateL0(L, N, V, Ra, F0, Roughness, Metallic, Albedo);
    }

    float shadow = 0;
    if (pc.softShadow == 1)
    {
        shadow = SoftShadowCalculation(FragPosLightSpace, bias);
    }
    else
    {
        shadow = HardShadowCalculation(FragPosLightSpace, bias);
    }

	vec3 F = FresnelSchlickRoughness(max(dot(N, V), 0.0), F0, Roughness);

	vec3 Ks = F;
	vec3 Kd = 1.0 - Ks;
	Kd *= 1.0 - Metallic;

	vec3 irradiance = texture(irradianceMap, N).rgb * pc.intensity;
	vec3 diffuse    = irradiance * Albedo;
	
    const float MAX_REFLECTION_LOD = 4.0;
    vec3 prefilteredColor = textureLod(prefilterMap, R, Roughness * MAX_REFLECTION_LOD).rgb;
    vec2 BRDF  = texture(brdfLUT, vec2(max(dot(N, V), 0.0), Roughness)).rg;
    vec3 specular = prefilteredColor * (F * BRDF.x + BRDF.y);

	vec3 ambient = (Kd * diffuse + specular) * AO;

    vec3 result = vec3(0);
	result = (1 - shadow) * L0 + ambient;

	vec3 hdrColor = result;

    // Reinhard è‰²è°ƒæ˜ å°„
    vec3 mapped = vec3(1.0) - exp(-hdrColor * pc.exposure);
    // gamma çŸ«æ­£ 
    mapped = pow(mapped, vec3(1.0 / pc.gamma));

    FragColor = vec4(mapped, 1.0);
}